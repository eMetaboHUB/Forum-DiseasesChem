PREFIX skos: <http://www.w3.org/2004/02/skos/core#>


SELECT DISTINCT <http://purl.obolibrary.org/obo/DOID_9281> ?cpd
WHERE { 
	
	?umls skos:exactMatch  <http://purl.obolibrary.org/obo/DOID_9281> .
        ?umls skos:exactMatch ?mesh .
        FILTER regex(?mesh, "http://id.nlm.nih.gov/mesh/")
        SERVICE <https://forum.semantic-metabolomics.fr/sparql/> { 
         ?cpd skos:related ?mesh. 
         }
}




PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX voc: <http://myorg.com/voc/doc#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX fabio:	<http://purl.org/spar/fabio/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX cid:   <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/CHEBI_>


SELECT distinct ?cpd ?mesh ?chebi ?role_label 
WHERE
{

	SERVICE <http://rdf.disgenet.org/sparql/> {
		?gda sio:SIO_000628 <http://identifiers.org/ncbigene/675>, ?disease ;
		     rdf:type sio:SIO_001122 ;
                     sio:SIO_000216 ?scoreIRI .
  
                ?scoreIRI sio:SIO_000300 ?score .
                FILTER (?score >= 0.9)
  
		?disease a ncit:C7057 .
		?disease skos:exactMatch ?mesh .
                FILTER(strstarts(str(?mesh), "http://id.nlm.nih.gov/mesh/"))
                
	}

	?mesh (meshv:treeNumber/meshv:parentTreeNumber*) ?tn .
	mesh:D009369 meshv:treeNumber ?tn .
	?cpd skos:related ?mesh .
	?cpd a ?chebi .
	?chebi rdfs:subClassOf [ a owl:Restriction ;
  		owl:onProperty <http://purl.obolibrary.org/obo/RO_0000087> ;
  		owl:someValuesFrom ?role ] .

	?role rdfs:subClassOf* chebi:23888 . 
	?role rdfs:label ?role_label 
}





PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX meshv: <http://id.nlm.nih.gov/mesh/vocab#>
PREFIX mesh: <http://id.nlm.nih.gov/mesh/>
PREFIX voc: <http://myorg.com/voc/doc#>
PREFIX cito: <http://purl.org/spar/cito/>
PREFIX fabio:	<http://purl.org/spar/fabio/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX cid:   <http://rdf.ncbi.nlm.nih.gov/pubchem/compound/>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX ncit: <http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX chebi: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX rh:<http://rdf.rhea-db.org/>
PREFIX up:<http://purl.uniprot.org/core/>
PREFIX taxon:<http://purl.uniprot.org/taxonomy/>

SELECT DISTINCT ?chebi ?PROTEIN_FULL_NAME ?gene_label

WHERE {

{
  select distinct ?chebi
  where
  {
    ?cid skos:related mesh:D010661 .
    ?cid rdf:type ?chebi .
    ?chebi rdfs:subClassOf [ a owl:Restriction ;
    owl:onProperty <http://purl.obolibrary.org/obo/RO_0000087> ;
    owl:someValuesFrom ?role ] .
    ?role rdfs:subClassOf* chebi:25212 .
  }
}


SERVICE <https://sparql.rhea-db.org/sparql> {
  
  ?RHEA_REACTION rdfs:subClassOf rh:Reaction .
  ?RHEA_REACTION rh:status rh:Approved .
  ?RHEA_REACTION rh:side / rh:contains / rh:compound / rh:chebi ?chebi .

}

SERVICE <https://sparql.uniprot.org/sparql> {
    # Rhea reactions catalyzed by UniProt proteins
    ?PROTEIN up:annotation/up:catalyticActivity/up:catalyzedReaction ?RHEA_REACTION .

    # UniProtKB/Swiss-Prot entries
    ?PROTEIN up:reviewed true .
    # Human entries
    ?PROTEIN up:organism taxon:9606 .
    # Protein name
    ?PROTEIN up:recommendedName/up:fullName ?PROTEIN_FULL_NAME .
    ?PROTEIN up:encodedBy ?gene .
    ?gene skos:prefLabel ?gene_label .

  }


}
